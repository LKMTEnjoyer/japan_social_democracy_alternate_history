title: Fundraising
new-page: true
view-if: fundraising_timer <= 0 and (not historical_mode)
on-arrival: fundraising_timer = 6; month_actions += 1
frequency: 300
is-card: true
card-image: img/fundraising.jpg
tags: party_affairs

= Fundraising

Most of our funds come from our affiliated unions, specially Sohyo.

- @maintain: Maintain our current dues.
- @reduce: Reduce dues.
- @increase: Increase dues.

@maintain
subtitle: [+ dues +] resources gained.
on-arrival: resources += dues; resources += 1 if sohyo_loyalty >= 0.75;

We maintain our current dues.

[? if sohyo_loyalty >= 0.75 : Because of how loyal Sohyo is, our fundraising efforts are more successful ?]

@reduce
subtitle: [+ dues - 1 +] resources gained.
choose-if: dues > 1
on-arrival: dues -= 1; resources += dues; resources += 1 if sohyo_loyalty >= 0.75; left_dissent -= 8; center_dissent -= 8; right_dissent -= 8; left_org_dissent -= 8; reconstructionist_dissent -= 8; soyho_loyalty += 0.08;

Our current members appreciate this, but no one decides to join a party because of the low dues.

The membership of Sohyo don't have to pay as much money and are happier

[? if sohyo_loyalty >= 0.75 : Because of how loyal Sohyo is, our fundraising efforts are more successful ?]

@increase
subtitle: [+ dues + 1 +] resources gained. [? if unemployed >= 15 : Given the present economic situation, this may be painful. ?][? if dues >= 4 : Dues are already rather high; increasing them might reduce our membership. ?]
on-arrival: dues += 1; resources += dues; resources += 1 if sohyo_loyalty >= 0.75; workers_jsp -= 3; unemployed_jsp -= 3; sohyo_loyalty -= 0.05; sohyo_loyalty -= 0.15 if unemployed >= 15; sohyo_loyalty -= 0.1 if dues == 4; workers_jsp -= 10 if unemployed >= 15; unemployed_jsp -= 10 if unemployed >= 15; workers_jsp -= 3*dues if dues >= 5; unemployed_jsp -= 3*dues if dues >= 5
view-if: dues <= 4

Our members grumble at this, and some of the less committed members leave. But now, we have access to additional resources. 

Sohyo is a bit unhappy. [? if sohyo_loyalty >= 0.75 : Because of how loyal Sohyo is, our fundraising efforts are more successful ?]

[? if unemployed >= 15 : Because of the presently high unemployment, our members are feeling the extra burden hard. ?]

