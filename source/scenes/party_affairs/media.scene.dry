title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/sangerbund.jpg

= Media

The JSP runs a veritable media empire, with many newspapers throughout every Japanese city and region. We could use the media for various purposes...

- @commercial
- @campaign
- @ideological
- @radio
- @tv
- @tv_2
- @tv_3
- @return: We should not do anything with media at the moment.
- @easy_discard

@commercial
title: Make our newspapers more commercialized by broadening their appeal beyond party supporters.
subtitle: This might upset ideological purists, but it may bring in more funds and perhaps expose the middle class to socialist views.
on-arrival: resources += 1; commercialized_media += 1; new_middle_jsp += 4*(1 - dissent); old_middle_jsp += 3*(1-dissent); left_dissent += 10; left_org_dissent += 10

Our commercialized newspapers are able to bring in more funds. However, the left faction and the Socialist Association express their displeasure.

@campaign
title: The purpose of our media is to elect more Social Democrats.
subtitle: Our newspapers will focus on the electoral campaign.
on-arrival: campaign_media += 1; workers_jsp += 4*(1 - dissent); unemployed_jsp += 2*(1-dissent);

Our campaign media is mostly reaching the workers who are already saturated with Socialist propaganda, but it must be doing *something*, or at least that is what we hope.

@ideological
title: We will have vibrant ideological debate within our newspapers.
subtitle: There will be space for all factions to air their views.
on-arrival: left_dissent -= 6; left_org_dissent -= 6; center_dissent -= 6; right_dissent -= 6; reconstructionist_dissent -= 6

This has been our media course historically, and it has successfully paved over the many tensions in our party.

@radio
title: Increase funding for our radio network.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: radio += 1; resources -= 1; new_middle_jsp += 5*(1 - dissent); old_middle_jsp += 3*(1 - dissent); workers_jsp += 3*(1 - dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

Our new radio network has a mix of popular and political programming, and is proving a success.

@tv
title: Why not fund a television channel?
subtitle: -2 resources
choose-if: resources >= 2
unavailable-subtitle: Not enough resources.
view-if: television = 0
on-arrival: television += 1; resources -= 2; new_middle_jsp += 4*(1 - dissent); old_middle_jsp += 3*(1 - dissent); workers_jsp += 2*(1 - dissent); left_dissent += 5; left_org_dissent += 5; socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent) if pacifism > 1; pro_republic += 3*(1-dissent) if democratization > 1

Our new television channel allows us to connect to people in a more intimate setting than before, spreading the message of socialism along with other popular content. Since most proletarians cannot afford televisions right now, we are reaching the middle class, a fact of which our comrades are painfully aware. 

@tv_2
title: We must keep on building up our television channel.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: television > 0 and television <= 3
on-arrival: television += 1; resources -= 1; new_middle_jsp += 5*(1 - dissent); old_middle_jsp += 3*(1 - dissent); workers_jsp += 3*(1 - dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

Our new television channel has a mix of popular and political programming, and is proving a success.

@tv_3
title: We must keep on building up our television channel.
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: television > 3
on-arrival: television += 1; new_middle_jsp += 5*(1 - dissent); old_middle_jsp += 4*(1 - dissent); workers_jsp += 4*(1 - dissent); rural_jsp += 3*(1 - dissent); unemployed_jsp += 2*(1-dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

Our television channel has become a successful tool for spreading our message.
