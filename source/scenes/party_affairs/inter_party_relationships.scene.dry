title: Inter-Party Relationships
new-page: true
view-if: inter_party_relationships_timer <= 0
tags: party_affairs
is-card: true
on-arrival: month_actions += 1; inter_party_relationships_timer = 6;
card-image: img/weimar_coalition_2.jpg
max-visits: 12

= Inter-Party Relationships

The JSP cannot govern by itself yet. Because we do not have an electoral majority, we need the support of other parties. Recently, our governments have consisted of the Weimar Coalition of the SPD, Center, and Democratic parties. We can reach out and build additional connections with these parties, or perhaps we can stand alone.

- @communist: Strengthen our bonds with the Communist Party.
- @labour_farmer: Strengthen our bonds with the Laborer-Farmer Party.
- @ldp: Improve our relations with the Liberal-Democratic Party.
- @lfp_merge: We can finally unite with the Laborer-Farmer Party!
- @jsp_alone: The JSP can stand alone for now.
- @easy_discard

@communist
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1;
go-to: jcp_1 if jcp_coalition_success = 0; jcp_2 if jcp_coalition_success = 1

@jcp_1
on-arrival: jcp_relation += 6*(1 - dissent);

The JCP is one of our allies, and they have consistently agreed to withdraw their candidates to avoid splitting the socialist vote.

@jcp_2
on-arrival: jcp_relation += 8*(1 - dissent);

The JCP is one of our allies, and they have consistently agreed to withdraw their candidates to avoid splitting the socialist vote. After the formation of the first socialist coalition in Japan, they are much more amenable to us.

@labour_farmer
subtitle: We may be able to merge with the LFP if we have good enough relations. -1 resources
view-if: lfp_united = 0
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
on-arrival: resources -= 1;
go-to: lfp_unfriendly if (reconstructionist_strength + right_strength >= left_strength + center_strength); lfp_friendly if (reconstructionist_strength + right_strength < left_strength + center_strength)

@lfp_unfriendly
on-arrival: lfp_relation += 4*(1 - dissent) 

With the right wing of our party currently dominating, improving relations with the LFP has gotten harder.

@lfp_friendly
on-arrival: lfp_relation += 8*(1 - dissent)

The left wing of our party supports cooperation with the LFP, and perhaps eventually uniting our parties.

@lfp_merge
subtitle: Their members will join our party. -2 resources
view-if: lfp_relation >= 45 and lfp_united = 0
choose-if: resources >= 2
unavailable-subtitle: [? if resources < 2 : Not enough resources. ?][? if lfp_relation < 60 : We do not have good enough relations. ?]
on-arrival: resources -= 2; lfp_united = 1; left_strength += 15; left_dissent -= 10; left_org_strength += 10; left_org_dissent -= 5; 

We have finally merged our parties.

@ldp
subtitle: This will be opposed by the left and the center. -1 resources
choose-if: resources >= 1 and (reconstructionist_strength + right_strength >= left_strength + center_strength)
unavailable-subtitle: [? if resources < 1 : Not enough resources. ?][? if (reconstructionist_strength + right_strength < left_strength + center_strength) : This policy is strongly opposed by most members of our party. ?]
on-arrival: resources -= 1; ldp_relation += 5*(1 - dissent); left_dissent += 5; left_org_dissent += 5; center_dissent += 5; reconstructionist_strength += 5; reconstructionist_dissent -= 5;

Improving relations with the LDP is very controversial in our party, given that they are our main opposition and a staunchly conservative and capitalist party. On the other hand, it is popular with the Reconstructionist wing of our party.

@jsp_alone

Whether it is because we *cannot* or *will not*, we do not make ties with other parties at this moment.

- @return: Continue...
